*_____________________________________________一直运行的指令，读取基址存在否处（插入动态强制色板）_______________________________________________
		org		$2345e
		jsr		_random_color

*_____________________________________________动态0D-1D载色(更改D3显色位,0E1E摩托)___________________________________________________________
	*===========================================【1】求DE\DF，并写入内存=============================================================
		org		$106600	
_random_color
		moveq		#$0,D0
		move.w		($20,A6),D0
		tst.w		($de,A6)			*是否没加载过
		beq			_rc
		move.b		($de,A6),($23,A6)
		rts		
_rc
		movem.l		D0-D6/A0-A3,-(A7)		;入栈					
		moveq		#$0,D1
		moveq		#$0,D0	
		cmpi.w		#$8,($20,A6)
		beq			_mj
		cmpi.w		#$c,($20,A6)
		beq			_lq
		cmpi.w		#$16C,($20,A6)		;寄生兽 tyrog2
		beq			_tyrog
		cmpi.w		#$14,($20,A6)			;屠夫
		beq			_tf
		cmpi.w		#$F4,($20,A6)			;屠夫
		beq			_tf
		cmpi.w		#$D0,($20,A6)			;屠夫刀x
		beq			_tf
		cmpi.w		#$18,($20,A6)			;狮子
		beq			_sz
		cmpi.w		#$28,($20,A6)			;胖子
		beq			_pangzi
		cmpi.w		#$13C,($20,A6)			;打龙胖子
		beq			_dlpang
		cmpi.w		#$34,($20,A6)			;步枪兵
		beq			_lr
		cmpi.w		#$38,($20,A6)			;匕首兵
		beq			_daobing
		cmpi.w		#$FC,($20,A6)			;匕首兵
		beq			_daobing
		cmpi.w		#$48,($20,A6)			;大个
		beq			_1B
		cmpi.w		#$50,($20,A6)			;猴子
		beq			_hz
		cmpi.w		#$100,($20,A6)			;扔雷猴子
		beq			_hz
		cmpi.w		#$134,($20,A6)			;扔雷猴子
		beq			_hz
		cmpi.w		#$60,($20,A6)			;暴龙
		beq			_KL
		cmpi.w		#$64,($20,A6)			;4b
		beq			_47b4
		cmpi.w		#$11c,($20,A6)			;幻影
		beq			_47b7
		cmpi.w		#$90,($20,A6)			;马云
		beq			_5B
		cmpi.w		#$A0,($20,A6)			;马云龙
		beq			_5BB
		cmpi.w		#$148,($20,A6)		;翼龙
		beq			_KL
		cmpi.w		#$150,($20,A6)			;大龙
		beq			_8B
		cmpi.w		#$118,($20,A6)			;大龙
		beq			_mt		
		cmpi.w		#$58,($20,A6)			;巨脚
		beq			_jj
		cmpi.w		#$74,($20,A6)			;迅猛龙
		beq			_KL
		cmpi.w		#$7c,($20,A6)			;3B
		beq			_3b
		cmpi.w		#$80,($20,A6)			;摩托
		beq			_3mt
		cmpi.w		#$8C,($20,A6)			;马云
		beq			_5B
		cmpi.w		#$130,($20,A6)		;寄生兽（幼体）
		beq			_tyrog6
		cmpi.w		#$120,($20,A6)		;寄生兽（尸体）
		beq			_tyrog6
		cmpi.w		#$10,($20,A6)			;寄生兽（BOSS）
		beq			_tyrog6		
		cmpi.w		#$178,($20,A6)			;宋小龙
		beq			_8B178
		cmpi.w		#$98,($20,A6)			;角龙
		beq			_KL
		cmpi.w		#$b4,($20,A6)			*受伤老者
		beq			_lt
		cmpi.w		#$cc,($20,A6)			*被砍龙
		beq			_bkl
		cmpi.w		#$B0,($20,A6)			;迷你角龙
		beq			_KL
		cmpi.w		#$BC,($20,A6)			;迷你雷龙
		beq			_KL
		cmpi.w		#$138,($20,A6)			;迷你角龙
		beq			_KL
		cmpi.w		#$140,($20,A6)			;迷你雷龙
		beq			_KL
		movem.l		(A7)+,D0-D6/A0-A3			;收工
		rts




*==============================马甲=========================================				
_mj		
		move.b		($26,A6),D0
		divu.w		#$2,D0
		move.b		_mj1data(PC,D0),D1		;取下表中随机一色板
		move.b		D1,($Df,A6)				*色板号写入敌人内存中
		bra    		_sxgy		
_mj1data
		dc.w		$0d0e,$3d0e,$3d0d,$3d0e			/马甲
		
*==============================刀兵、飞刀手=========================================				
_daobing		
		move.b		#$15,D1
		move.b		D1,($Df,A6)				;色板号也写入敌人内存中
		bra    		_sxgy		
*==============================胖子=========================================				
_pangzi		
		move.b		($26,A6),D0
		divu.w		#$2,D0
		move.b		_pzdata(PC,D0),D1		;取下表中随机一色板
		move.b		D1,($Df,A6)				;色板号也写入敌人内存中
		bra    		_sxgy		
_pzdata
		dc.l		$12131493
		
*==============================打睡龙13C=========================================				
_dlpang	
		move.b		#$75,D1
		move.b		D1,($Df,A6)				;色板号也写入敌人内存中
		bra    		_sxgy				
*===========================猴子、扔雷猴=======================================		
_hz	
		move.b		($26,A6),D0
		divu.w		#$2,D0
		move.b		_hzdata(PC,D0),D1		;取下表中随机一色板
		move.b		D1,($Df,A6)				;色板号也写入敌人内存中
		bra    		_sxgy			
_hzdata
		dc.w		$1819,$1819
				
*===========================恐龙=======================================		
_KL		
		jsr     $119c					;随机程序
		andi.w	#$f,D0				
		move.b	_KLdata(PC,D0),D1		;取下表中随机一色板
		move.b	D1,($Df,A6)				;色板号也写入敌人内存中
		bra    	_sxgy		
_KLdata
		dc.w		$2b2d,$2e2f,$3b9e,$2731,$3572,$2d3b,$3527,$9e31			/恐龙
		
*===========================马云=======================================	
_5B
		move.b		($4d9,A5),D0		;取下表中随机一色板
		move.b	    _5Bdata(PC,D0),D1		;取下表中随机一色板
		move.b		D1,($Df,A6)				;色板号也写入敌人内存中
		bra    		_sxgy
_5Bdata		
            dc.l        $24404173,$741d7697
*===========================铁头龙=======================================	
_5BB
		move.b		($4d9,A5),D0		;取下表中随机一色板
		move.b	    _5BBdata(PC,D0),D1		;取下表中随机一色板
		move.b		D1,($Df,A6)				;色板号也写入敌人内存中
		bra    		_sxgy
_5BBdata
        dc.l        $251d2473,$17743629			
*===========================178=======================================
_8B178
		move.b		#$33,D1		;取下表中随机一色板
		move.b		D1,($Df,A6)				;色板号也写入敌人内存中
		bra    		_sxgy
*===========================屠夫=======================================
_tf
		move.b		($4d9,A5),D0		;取下表中随机一色板
		move.b	    _tfdata(PC,D0),D1		;取下表中随机一色板
		move.b		D1,($Df,A6)				;色板号也写入敌人内存中
		bra    		_sxgy		
_tfdata
        dc.l        $321d2473,$17747729		
*===========================6BOSS=======================================
_tyrog6
		cmpi.b		#$4,($4d9,A5)
		beq			_must_8D			;第五关直接黑色色板
		move.b		($26,A6),D0
		move.b		_tyrog6data(PC,D0),D1		;取下表中随机一色板
		move.b		D1,($Df,A6)				;色板号也写入敌人内存中
		bra    		_sxgy
_tyrog6data
		dc.l		$72317171
*===========================跳跳龙=======================================
_tyrog
		move.b		($4d9,A5),D0		;取下表中随机一色板
		move.b	    _tyrogdata(PC,D0),D1		;取下表中随机一色板
		move.b		D1,($Df,A6)				;色板号也写入敌人内存中
		bra    		_sxgy
_tyrogdata
        dc.l        $2a2d2e3b,$27093171
*===========================狮子=======================================
_sz
		move.b		($4d9,A5),D0		;取下表中随机一色板
		move.b	    _szdata(PC,D0),D1		;取下表中随机一色板
		move.b		D1,($Df,A6)				;色板号也写入敌人内存中
		bra    		_sxgy		
_szdata
        dc.l        $251d2473,$17743629		
*===========================3BOSS=======================================
_3b
		jsr     		$119c					;随机程序
		andi.w		#$1f,D0		    			;余数在高12位，交叉下
		move.b		_3bdata(PC,D0),D1		;取下表中随机一色板
		move.b		D1,($Df,A6)				;色板号也写入敌人内存中
		bra    		_sxgy		
_3bdata
		dc.w		$0100,$0203,$183e,$5b12,$1314,$1516,$7593,$9496
		dc.w		$9899,$1934,$953a,$3c16,$0100,$0203,$183e,$5b12		*3BOSS肉身		
*===========================链球=======================================
_lq
		move.b		#$1a,D1					;取下表中随机一色板
		move.b		D1,($Df,A6)				;色板号也写入敌人内存中
		bra    		_sxgy		
*===========================猎人=======================================
_lr
		move.b		($26,A6),D0
		divu.w		#$2,D0
		move.b		_lrdata(PC,D0),D1		;取下表中随机一色板
		move.b		D1,($Df,A6)				;色板号也写入敌人内存中
		bra    		_sxgy		
_lrdata
		dc.l		$0f101111

*===========================1BOSS=======================================
_1B
		tst.w		($26,A6)
		beq		    _true1b
		move.b		($4d9,A5),D0		;取下表中随机一色板
		move.b	    _1Bdata(PC,D0),D1		;取下表中随机一色板
		move.b		D1,($Df,A6)				;色板号也写入敌人内存中
		bra    		_sxgy
_1Bdata
        dc.l        $9f1b1f9b,$9c404142
_true1b
		move.b		#$1C,D1		;取下表中随机一色板
		move.b		D1,($Df,A6)				;色板号也写入敌人内存中
		bra    		_sxgy

*===========================大龙=======================================
_8B
		jsr     		$119c					;随机程序
		andi.w		#$f,D0					;求余, 即限制范围( )
		move.b		_8Bdata(PC,D0),D1		;取下表中随机一色板
		move.b		D1,($Df,A6)				;色板号也写入敌人内存中
		bra    		_sxgy		
_8Bdata
		dc.w		$1e2a,$2b2d,$2e2f,$3b08,$099e,$2731,$3571,$7278	*大龙

*===========================4幻影=======================================
_47b4
		move.b		($4d9,A5),D0		;取下表中随机一色板
		move.b	    _47b4data(PC,D0),D1		;取下表中随机一色板
		move.b	    D1,($Df,A6)				;色板号也写入敌人内存中
		bra    	    _sxgy		
_47b4data
        dc.l        $3aa21221,$14169493
*===========================7幻影=======================================
_47b7
		move.b		($4d9,A5),D0		;取下表中随机一色板
		move.b	    _47b7data(PC,D0),D1		;取下表中随机一色板
		move.b	D1,($Df,A6)				;色板号也写入敌人内存中
		bra    	_sxgy		
_47b7data
        dc.l        $113a9493,$16a22212
*===========================被砍龙=======================================
_bkl
		move.b	#$36,D1					;取下表中随机一色板
		move.b	D1,($Df,A6)				;色板号也写入敌人内存中
		bra    	_sxgy		
*===========================巨脚=======================================
_jj
		move.b	#$30,D1					;取下表中随机一色板
		move.b	D1,($Df,A6)				;色板号也写入敌人内存中
		bra    	_sxgy		
*===========================受伤老者=======================================
_lt
		move.b	#$2C,D1					;取下表中随机一色板
		move.b	D1,($Df,A6)				;色板号也写入敌人内存中
		;bra    	_sxgy		
*================================================================================================
_sxgy	
		moveq	#$0,D0
		movea.l	#$FFFFDDF4,A3			*//A3置为DDF4（第0个）		
_next
		suba.l	#$E0,A3					;偏移到下一个敌人基址
		cmpa.l	#$FFFFC8f4,A3			;是否小于C8F4
		blt     _apply_cp_db					*'小于C8F4，说明无人共用色板号'开始申请显色位		
		cmpa.l	A3,A6					;是否是此人地址
		beq		_next					//是，下一个敌人		
		move.b	($df,A3),D0				;色板号2给D1
		beq		_next					*D1为0就别检测了，下一个
		cmp.w	D0,D1					;检测是否跟某色板号是否相同
		bne		_next					*不同，下一个
*----------------------------------找到相同-------------------------------------
		move.b	($de,A3),($de,A6)		*找到一致的色板号，与之共用显存位
		move.b	($de,A3),($23,A6)		*找到一致的色板号，与之共用显存位
		move.b	($de,A6),D0				*显存位放在D0，色板号放在D1	
_write
		andi.b		#$1f,D0						;压缩到20内
		lsl.w 		#5,D0					;转换成显存相关
		movea.l 		#$914000,A1				;显色存基址
		adda.l		D0,A1					;转换成显存相关
		andi.w		#$ff,D1						;去掉低34位
		lsl.w  		#5,D1						;取得色板号X20
		movea.l  		#$b8172,A3					;色板数据基址(第一个是白人是色板)
		adda.l		D1,A3						;得出此敌人色板数据
		movem.l		(A3),D0-D6/A2
		movem.l		D0-D6/A2,(A1)
		or.l			#$f000f000,(A1)+		/设置亮度
		or.l			#$f000f000,(A1)+
		or.l			#$f000f000,(A1)+
		or.l			#$f000f000,(A1)+
		or.l			#$f000f000,(A1)+
		or.l			#$f000f000,(A1)+
		or.l			#$f000f000,(A1)+
		or.l			#$f000f000,(A1)+
		movem.l		(A7)+,D0-D6/A0-A3				;收工		
		rts

*------------------------------申请显色位（8f-9f ）-------------------------------------
_apply_cp_db
		move.b	#$8f,D0						;初始显存位，强制色板8f开始
_xsw_jc_ks
		movea.l	#$FFFFDD14,A3				;载入敌人起始基址
_jzjc_next
		cmpa.l	#$FFFFC8f4,A3				;是否小于FFFFC8f4
		bLT		_jcd_ok						;小于FFFFC8f4，说明场上敌人没人用此显色位，同意使用
		cmp.B	($de,A3),D0					;某敌人是否使用D0(某显色位)
		beq		_jcd_j						;此显色位已被使用，得换色位重新检测
		suba.l	#$E0,A3						;基址偏移-E0
		bra		_jzjc_next					;检测下一个敌人基址

_jcd_j
		addq	#1,D0						;已被使用D0+1检测重新检测
_jcd_j_9e
		cmpi.b	#$9e,D0						;是否摩托色位
		beq		_jcd_j						;
_jcd_j_9c
		cmpi.b	#$9c,D0						;是否9c，9c是水里占用
		beq		_jcd_sfSL					;是检测是否水里
_jc_sw_jx
		cmpi.b	#$9f,D0						;超过9f还是退出吧
		bgt		_must_8D
		bra		_xsw_jc_ks					;重新开始检测 DD14~C8f4
_must_8D
		move.b	#$8D,D0						*显色位已满，强制使用通用显色位8D，强制色板33，通用黑色
		move.b	#$33,D1
		move.b	D1,($df,A6)
		move.b	D0,($de,A6)					;写入色板位
		move.b	D0,($23,A6)					;写入强制色板
		bra		_write
	;===========================================================================================
_jcd_sfSL
        cmpi.b  #1,($4d9,A5)            		;检测是否2关
		beq     _jc_cj26
        cmpi.b  #5,($4d9,A5)            		;检测是否6关
		beq     _jc_cj26
		bra		_jc_sw_jx					;继续
		;---------------------------------------------------
_jc_cj26
		cmpi.b  #1,($786,A5)					;是否2场景
		bne     _jc_sw_jx					;否继续
		addq	#1,D0						;都是D0加1，绕过
		bra		_jc_sw_jx					;继续
	*===========================↓↓↓===申请到色位，并写入===↓↓↓===============================
_jcd_ok
		move.b	D0,($de,A6)					;写入色板位
		move.b	D0,($23,A6)					;写入强制色板
		bra		_write

*=========================================3BOSS摩托=================================================================
_3mt
		move.b	#$33,($df,A6)
		clr.b	($23,A6)
		movea.l	#$9143c0,A1
		move.l	#$F111F333,(A1)+					;摩托1E
		move.l	#$F555F777,(A1)+
		move.l	#$F999FBBB,(A1)+
		move.l	#$FDDDFFFF,(A1)+
		move.l	#$FFEAFFD2,(A1)+
		move.l	#$FF93FE22,(A1)+
		move.l	#$FC06F908,(A1)+
		move.l	#$F607FA80,(A1)+
		movem.l	(A7)+,D0-D6/A0-A3				;收工
		rts
		
		
*==========================================摩托车===================================================
_mt	
		movea.l	#$9143c0,A1
		move.l	#$F111F333,(A1)+					;摩托1E
		move.l	#$F555F777,(A1)+
		move.l	#$F999FBBB,(A1)+
		move.l	#$FDDDFFFF,(A1)+
		move.l	#$FFEAFFD2,(A1)+
		move.l	#$FF93FE22,(A1)+
		move.l	#$FC06F908,(A1)+
		move.l	#$F607FA80,(A1)+				
		jsr     $119c						;随机程序
		andi.w	#$3,D0					;求余, 即限制范围( )
		move.b	_mjdata(PC,D0),D1			;取下表中随机一色板
		move.b	D1,($Df,A6)				;色板号也写入敌人内存中
		bra		_bysb
_mjdata
		dc.w		$0d0e,$3d0e,$3d0d			/马甲
_bysb
		movea.l	#$9141C0,A1				;显色存基址	E
		andi.w	#$ff,D1						;去掉低34位
		lsl.w	#5,D1						;取得色板号X20
		movea.l	#$b8172,A3
		adda.l	D1,A3
		movem.l	(A3),D0-D6/A2
		movem.l	D0-D6/A2,(A1)
		or.l		#$f000f000,(A1)+		/设置亮度
		or.l		#$f000f000,(A1)+
		or.l		#$f000f000,(A1)+
		or.l		#$f000f000,(A1)+
		or.l		#$f000f000,(A1)+
		or.l		#$f000f000,(A1)+
		or.l		#$f000f000,(A1)+
		or.l		#$f000f000,(A1)+
		movem.l	(A7)+,D0-D6/A0-A3				;收工
		rts
;=================================================================================		
_under0d
		move.w  (A1)+, D2			;把A1提前，反正都得走这一步
		move.w  (A1)+, D3			;因为要提前判断D3是否小于D
		cmpi.w	#$64,($20,A0)
		beq		_014448JX
		cmpi.b	#$d,D3				;是否小于D
		blt	_014404				;小于D不用强制色板
_014448JX
		move.b  ($23,A0),D0
_014450
		jmp		$01451e				;继续
_014404
		jmp		$0144d2				;跳到非强制
;================================================================================
_clrobj
		move.b  D0, ($87,A6)		;其他的
		move.b  D0, ($96,A6)		;其他的
		clr.w    ($de,A6) 			;色板号位
		rts
;==================================================================================
_clrde
		clr.w	($5df2,A5)
		clr.w	($5d12,A5)
		clr.w	($5c32,A5)
		clr.w	($5b52,A5)
		clr.w	($5a72,A5)
		clr.w	($5992,A5)
		clr.w	($58b2,A5)
		clr.w	($57d2,A5)
		clr.w	($56f2,A5)
		clr.w	($5612,A5)
		clr.w	($5532,A5)
		clr.w	($5452,A5)
		clr.w	($5372,A5)
		clr.w	($5292,A5)
		clr.w	($51b2,A5)
		clr.w	($50d2,A5)
		clr.w	($4ff2,A5)
		clr.w	($4f12,A5)
		clr.w	($4e32,A5)
		clr.w	($4d52,A5)
		clr.w	($4c72,A5)
		clr.w	($4b92,A5)
		clr.w	($4ab2,A5)
		clr.w	($49d2,A5)
		jsr		$892c
		rts
;==================================================================================		
_cs
	move.b	#$8d,($23,A6)
	move.b	#$8d,($23,A0)
	move.w	($8,A0),($8,A6)
	rts
_111
	jsr		$8bd24
	movem.l D0-D6/A0-A3,-(A7)			;入栈(目的借用寄存器)
	moveq	#$0,D1
	move.b	#$27,D1
	lsl.w   #5,D1
	movea.l #$9141a0,A1			;通用显色位8D
	movea.l #$b8172,A3
	adda.l  D1,A3
	movem.l (A3),D0-D6/A2
	movem.l D0-D6/A2,(A1)
	or.l		#$f000f000,(A1)+		/设置亮度
	or.l		#$f000f000,(A1)+
	or.l		#$f000f000,(A1)+
	or.l		#$f000f000,(A1)+
	or.l		#$f000f000,(A1)+
	or.l		#$f000f000,(A1)+
	or.l		#$f000f000,(A1)+
	or.l		#$f000f000,(A1)+
	movem.l	(A7)+,D0-D6/A0-A3			;收工
	rts
_carcolor
	;dc.w	$0809,$1e27,$282a,$2d2e,$3b71,$7827
;=====================================================================================================	
_clr3b
	move.w	#$7c,($20,A0)
	clr.w	($de,A0)
	clr.w	($de,A6)
	clr.w	($de,A1)
	rts
*===========================================小兵死后一些内存数据清零===========================================
		org		$492e				*小兵死后一些内存数据清零
		nop
		jsr		_clrobj
*-----------------------------------0d以下不用强制色板------------------------------------------
		org		$14516
		nop
		jmp		_under0d
*=============================续币后重刷色板时，清空OBJ内存位显存色板号（$de,A6）================================
		org		$ef0c
		jsr		_clrde
*===========================================NOP掉一些自带的强制色板===========================================
		org		$655ba				;tyrog2
		dc.w		$4e71,$4e71,$4e71
		org		$655ca				;tyrog2
		dc.w		$4e71,$4e71,$4e71
		org		$655e0				;tyrog2
		dc.w		$4e71,$4e71,$4e71
		
		org		$36b9a
		nop
		nop
		nop
		
		org		$36baa
		nop
		nop
		nop
		
		org		$36bc0
		nop
		nop
		nop

*===========================================凯迪拉克160===========================================================
	ORG		$8c4e6
	jsr		_cs
	
    org		$2a150		*电台召唤汽车，运行一次的指令
    jsr		_111
*=========================================3BOSS（7C）变为7C+80=====================================================================	
	org		$4fd08
	jsr		_clr3b
*====================================================================================================================================




















*~Font name~Courier New~
*~Font size~10~
*~Tab type~1~
*~Tab size~4~
